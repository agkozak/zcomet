#autoload

############################################################
# The `list' command
#
# Displays loaded plugins, added FPATH elements, sourced
# snippets, and defined triggers
#
# Arguments:
#   None
# Returns:
#   0 if there is anything to report, otherwise 1
############################################################

local success plugin abbr

if (( ${#zsh_loaded_plugins} )); then
  success=1
  print -P '%B%F{yellow}Plugins:%f%b'
  for plugin in ${(o)zsh_loaded_plugins[@]}; do
		if [[ $plugin == /* ]]; then
			print -n -- "  ${plugin}"
		else
			print -n '  '
			print -n -- "${plugin%%/*}/${${plugin#*/}%%/*}"
			abbr=${plugin#${plugin%%/*}/${${plugin#*/}%%/*}}
			[[ -n $abbr ]] && print -n -- " ${abbr#/}"
		fi
		print -n -- "${ZCOMET_PLUGINS[$plugin]}"
		print
  done
fi
(( ${#ZCOMET_FPATH} )) &&
  success=1 &&
  print -P '%B%F{yellow}FPATH elements:%f%b' &&
  print -l -f '  %s\n' "${(o)ZCOMET_FPATH[@]}"
(( ${#ZCOMET_SNIPPETS} )) &&
  success=1 &&
  print -P '%B%F{yellow}Snippets:%f%b' &&
  print -l -f '  %s\n' "${(o)ZCOMET_SNIPPETS[@]}"
(( ${#ZCOMET_TRIGGERS} )) &&
  success=1 &&
  print -P '%B%F{yellow}Triggers:%f%b' &&
	print -n '  '
	print -z -f '%s, ' "${(o)ZCOMET_TRIGGERS[@]}"
	read -z && print "${REPLY%,}" | fold -s -w $COLUMNS

(( success )) && return 0 || return 1

# vim: ft=zsh:ts=2:sts=2:sw=2
